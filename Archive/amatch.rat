#-H- AMATCH.RAT 1455 1980 103 2153
INCLUDE MDEF
 
# AMATCH  (NON-RECURSIVE) - LOOK FOR MATCH STARTING AT LIN(FROM)
   INTEGER FUNCTION AMATCH(LIN, FROM, PAT)
   CHARACTER LIN(MAXLINE), PAT(MAXPAT)
   INTEGER OMATCH, PATSIZ
   INTEGER FROM, I, J, K, OFFSET, STACK
   INCLUDE CSUBS
 
   STACK = 0
   OFFSET = FROM      # NEXT UNEXAMINED INPUT CHARACTER
   FOR (J = 1; J <= MAXSUBS; J = J + 1) <%# CLEAR PARTIAL MATCH RESULTS
      BPOS(J) = OFFSET
      EPOS(J) = OFFSET
      !
   FOR (J = 1; PAT(J) ^= EOS; J = J + PATSIZ(PAT, J))
      IF (PAT(J) == CLOSURE) <      # A CLOSURE ENTRY
         STACK = J
         J = J + CLOSIZE      # STEP OVER CLOSURE
         FOR (I = OFFSET; LIN(I) ^= EOS; )   # MATCH AS MANY AS
            IF (OMATCH(LIN, I, PAT, J) == NO)   # POSSIBLE
               BREAK
         PAT(STACK+COUNT) = I - OFFSET
         PAT(STACK+START) = OFFSET
         OFFSET = I      # CHARACTER THAT MADE US FAIL
         !
      ELSE IF (OMATCH(LIN, OFFSET, PAT, J) == NO) <   # NON-CLOSURE
         FOR ( ; STACK > 0; STACK = PAT(STACK+PREVCL))
            IF (PAT(STACK+COUNT) > 0)
               BREAK
         IF (STACK <= 0) <      # STACK IS EMPTY
            AMATCH = 0      # RETURN FAILURE
            RETURN
            !
         PAT(STACK+COUNT) = PAT(STACK+COUNT) - 1
         J = STACK + CLOSIZE
         OFFSET = PAT(STACK+START) + PAT(STACK+COUNT)
         !
      # ELSE OMATCH SUCCEEDED
   EPOS(1) = OFFSET
   AMATCH = OFFSET
   RETURN      # SUCCESS
   END
